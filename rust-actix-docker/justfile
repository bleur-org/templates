set dotenv-load := true
set shell := ["bash", "-uc"]

dc := "podman-compose"
dir := "docker"
dev_cmd := dc + " -f " + dir + "/docker-compose.dev.yml"
prod_cmd := dc + " -f " + dir + "/docker-compose.yml"
db_url := "postgres://${DB_USERNAME}:${DB_PASSWORD}@postgres:5432/${DB_NAME}"

default: dev

# aliases
dev:
  just up
  just migrate

prod:
  just up prod
  just migrate prod

# Generic
c env cmd:
  if [ "{{ env }}" = "prod" ]; then {{ prod_cmd }} {{ cmd }}; else {{ dev_cmd }} {{ cmd }}; fi

up env="dev":
  just c {{ env }} "up -d"

down env="dev":
  just c {{ env }} down

logs env="dev":
  just c {{ env }} "logs -f"

restart env="dev":
  just c {{ env }} restart

build env="dev":
  just c {{ env }} build

ps env="dev":
  just c {{ env }} ps

shell env="dev":
  if [ "{{ env }}" = "prod" ]; then podman exec -it prod_app bash; else podman exec -it dev_app bash; fi

db env="dev":
  if [ "{{ env }}" = "prod" ]; then podman exec -it prod_postgres psql -U "$DB_USERNAME" -d "$DB_NAME"; else podman exec -it dev_postgres psql -U "$DB_USERNAME" -d "$DB_NAME"; fi

migrate env="dev":
  @echo "Running migrations for {{ env }}..."
  if [ "{{ env }}" = "prod" ]; then \
    podman exec prod_app diesel migration run --migration-dir /app/migrations --database-url {{ db_url }}; \
  else \
    {{ dev_cmd }} run --rm -e DATABASE_URL={{ db_url }} app \
      diesel migration run --migration-dir /workspace/crates/database/migrations; \
  fi

migrate-fresh env="dev":
  @echo "Redoing last migration for {{ env }}..."
  if [ "{{ env }}" = "prod" ]; then \
    podman exec prod_app diesel migration redo --migration-dir /app/migrations --database-url {{ db_url }}; \
  else \
    {{ dev_cmd }} run --rm -e DATABASE_URL={{ db_url }} app \
      diesel migration redo --migration-dir /workspace/crates/database/migrations; \
  fi

clean:
  rm -rf target result config.toml diesel.toml src/
  {{ dev_cmd }} down -v --remove-orphans || true
  {{ prod_cmd }} down -v --remove-orphans || true

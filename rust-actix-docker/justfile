# You can set Podman here
DOCKER_COMPOSE := "docker-compose"

set dotenv-filename := ".env"
set dotenv-load
set export

[doc('Start default server cli with default config')]
start:
  cargo run server run ./config.toml

[doc('Start specific package in the project')]
run crate:
  cargo run --bin {{crate}}

[doc('Lint your rust codebase with clippy')]
lint:
  cargo clippy

[doc('Format your codebase with rust formatter')]
format:
  cargo fmt

[doc('Generate default config.toml file')]
config-generate:
  cargo run config generate ./config.toml

[working-directory('./crates/database')]
[confirm("Are you sure you want to do migration?")]
[doc('Perform migration with working database')]
migrate:
  diesel migration run

[doc('Start all Docker services (postgres + app)')]
docker-up:
  {{DOCKER_COMPOSE}} up -d

[doc('Stop all Docker services')]
docker-down:
  {{DOCKER_COMPOSE}} down

[doc('View logs from all Docker services')]
docker-logs:
  {{DOCKER_COMPOSE}} logs -f

[doc('View logs from specific service')]
docker-logs-service service:
  {{DOCKER_COMPOSE}} logs -f {{service}}

[doc('Restart all Docker services')]
docker-restart:
  {{DOCKER_COMPOSE}} restart

[doc('Build Docker images')]
docker-build:
  {{DOCKER_COMPOSE}} build

[doc('Start only postgres database')]
db-up:
  {{DOCKER_COMPOSE}} up -d postgres

[doc('Stop only postgres database')]
db-down:
  {{DOCKER_COMPOSE}} stop postgres

[doc('Access postgres shell')]
db-shell:
  {{DOCKER_COMPOSE}} exec postgres psql -U temp -d temp

[confirm("Are you sure you want to delete all data & records?!")]
[doc('Clean up all mess created by development environment')]
clean: docker-down
  rm -rf docker-data
  rm -rf .env
  rm -rf target
  rm -rf result

[confirm("Are you sure you want to reset Docker volumes?")]
[doc('Full Docker cleanup including volumes')]
docker-reset:
  {{DOCKER_COMPOSE}} down -v
  rm -rf docker-data
